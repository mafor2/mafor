// -*- kpp -*- kpp mode for emacs

// Replacement files allow you to modify gas.eqn. Please note: If you apply
// any *.rpl files to the reaction mechanism, you do so at your own risk.
// The files provided here are mostly under development. For more
// information, please contact the authors mentioned in the *.rpl files.

// IMPORTANT: When adding a new reaction, please assign a PRELIMINARY
// equation tag by adding your initials as a suffix, e.g. John Doe could
// use "<G0001JD>".

// Syntax of this replacement file is similar to kpp syntax, but...
// - Only the commands "#REPLACE" and "#ENDREPLACE" are allowed.
// - Comments must start with "//", curly braces ("{}") cannot be used here.

// Known limitations:
// - The equation tag (e.g. "<G9876>") in the input equation file
//   (gas.eqn) must be the first (non-whitespace) item in the line. If it is
//   preceded by a comment in curly braces, replace.awk will not be able to
//   find it.
// - New reactions will always be at the end.

// Here, reaction G4110 is replaced by a modified reaction:
//#REPLACE <G4110>
//<> CO + OH = HO2 + CO2 : {%StTrG} 1.57E-13 + cair*3.54E-33; {&1628}
//#ENDREPLACE

// To delete a reaction, specify an empty replacement:
//#REPLACE <G4200>
//#ENDREPLACE

// An error message is given if the specified reaction does not exist.
// Activate the following lines to test if the error is detected:
// #REPLACE <G9999>
// <a> CO + OH = HO2 + CO2 : {%StTrG} 1.57E-13 + cair*3.54E-33; {&1628}
// #ENDREPLACE

// use "*" as the last character to delete several reactions:
//#REPLACE <G300*>
//#ENDREPLACE

// use "*" as the last character to replace several reactions. Here,
// the complete equation tag must be given in the replacement:
//#REPLACE <G98*>
//<G9801> Dummy + OH  = CH3O2 + H2O : {%StG} 1E-13; {&&}
//<G9802> Dummy + O1D = CH3O2 + H2O : {%StG} 2E-13; {&&}
//<G9803> Dummy + O3  = CH3O2 + H2O : {%StG} 3E-13; {&&}
//#ENDREPLACE

// Additional *.rpl files can be included. 
// - The include file must be in the rpl/include/ directory
// - The included files cannot contain include commands (no nesting).
// - Type filename with or without the suffix ".rpl", e.g.:
//   #include myrplfile1
//   #include myrplfile2.rpl

// ---------------------------------------------------------------------------------------------------


// MAFOR N-O CHEMISTRY
// N + O (#G31nn)
#REPLACE <>
<G3126MK>  NO  + NO2          = N2O3        : {%StTrG} 7.9E-12;  {&1610}
<G3127MK>  NO2 + NO2          = N2O4        : {%StTrG} 1.0E-12;  {&1610}
<G3128MK>  N2O3               = NO  + NO2   : {%StTrG} 3.6E8;    {&1610}
<G3129MK>  N2O4               = NO2 + NO2   : {%StTrG} 4.4E6;    {&1610}
#ENDREPLACE



// MAFOR AMINE CHEMISTRY
// N + C (#G41nn - #G46nn)

// 1C (#G41nn)
#REPLACE <>
<G4170aMK> MMA        + OH    = .3 CH2NH + .3 HO2 + .1 MMAO2 + .6 CH3NH          : {%TrGAmi} (1-ya_soan1-ya_soan2-ya_soan5)*k_MMA_OH*xgcamin; {&26220}
<G4170bMK> MMA        + OH    = BSOV + LCARBON + LNITROGEN                       : {%TrGAmi} ya_soan1*k_MMA_OH;    {&26220}
<G4170cMK> MMA        + OH    = BLOV + LCARBON + LNITROGEN                       : {%TrGAmi} ya_soan2*k_MMA_OH;    {&26220}
<G4170dMK> MMA        + OH    = BELV + LCARBON + LNITROGEN                       : {%TrGAmi} ya_soan5*k_MMA_OH;    {&26220}
<G4171MK>  CH3NH      + NO2   = .5 MMNNO2 + .5 CH2NH + .5 HONO                   : {%TrGAmi} 10*1.1E-12*xgcnno{§}; {&26220}
<G4172MK>  CH3NH      + O2    = CH2NH + HO2                                      : {%TrGAmi} 2.4E-17{§};           {&2596}
<G4173MK>  MMAO2      + NO    = H2NCHO + HO2 + NO2                               : {%TrGAmi} 0.5*7.7E-12{§};       {&2596}
<G4174MK>  H2NCHO     + OH    = HNCO + H2O                                       : {%TrGAmi} k_H2NCHO_OH;          {&26220}
<G4175MK>  MMA        + CH2NH = CH2NCH3 + NH3                                    : {%TrGAmi} 4.0E-17{§}*wall;      {&26220}
#ENDREPLACE


// 2C (#G42nn)
#REPLACE <>
<G4200aMK> MEA        + OH  = 0.05 H2NCH2CHO + 0.8 MEABO2 + 0.15 MEAN + 0.05 HO2 : {%TrGAmi} (1-ya_soan1-ya_soan2-ya_soan5)*k_MEA_OH*fkoh_mea*xgcamin; {&2594}
<G4200bMK> MEA        + OH  = BSOV + 2 LCARBON + LNITROGEN                       : {%TrGAmi} ya_soan1*k_MEA_OH*fkoh_mea; {&2594}
<G4200cMK> MEA        + OH  = BLOV + 2 LCARBON + LNITROGEN                       : {%TrGAmi} ya_soan2*k_MEA_OH*fkoh_mea; {&2594}
<G4200dMK> MEA        + OH  = BELV + 2 LCARBON + LNITROGEN                       : {%TrGAmi} ya_soan5*k_MEA_OH*fkoh_mea; {&2594}
<G4201MK>  H2NCH2CHO  + OH  = 0.8 H2NCH2CO3 + 0.2 H2NCHO2CHO + H2O               : {%TrGAmi} k_AAC_OH;          {&2594}
<G4202MK>  H2NCH2CO3  + NO  = MMAO2 + CO2 + NO2                                  : {%TrGAmi} KAPNO;             {&2594}
<G4203MK>  H2NCHO2CHO + NO  = H2NCOCHO + HO2 + NO2                               : {%TrGAmi} KRO2NO*2;          {&2594}
<G4204MK>  H2NCOCHO   + OH  = H2NCOCO3 + H2O                                     : {%TrGAmi} k_OXA_OH;          {&2594}
<G4205MK>  H2NCOCO3   + NO  = H2NCHO + CO2 + NO2                                 : {%TrGAmi} KAPNO;             {&2594}
<G4206MK>  MEABO2     + NO  = MEABO + NO2                                        : {%TrGAmi} KRO2NO;            {&2594}
<G4207MK>  MEABO      + O2  = H2NCOCH2OH + HO2                                   : {%TrGAmi} k_RO_O2*0.25;      {&2594}
<G4208MK>  MEABO            = H2NCHO + HCHO                                      : {%TrGAmi} k_DEC*0.2;         {&2594}
<G4209MK>  H2NCOCH2OH + OH  = H2NCOCHO + HO2                                     : {%TrGAmi} k_HAC_OH;          {&2594}
<G4210MK>  MEAN       + NO2 = 0.5 MEANNO2 + 0.5 HNCHCH2OH + 0.5 HONO             : {%TrGAmi} 1.4E-13{§} *xgcnno;   {&2594}
<G4211MK>  MEAN       + O2  = HNCHCH2OH + HO2                                    : {%TrGAmi} 1.2E-19{§};        {&2594}
<G4212MK>  MEAN       + NO  = MEANNO                                             : {%TrGAmi} 8.5E-14{§} *xgcnno;   {&2594}
<G4213MK>  MEANNO2    + OH  = MEANHA + HO2                                       : {%TrGAmi} k_NITRAMA_OH;      {&2594}
<G4214MK>  HNCHCH2OH  + OH  = H2NCOCH2OH + HO2                                   : {%TrGAmi} 3.00E-13{§};       {&2594}
#ENDREPLACE

#REPLACE <>
<G4220aMK> DMA    + OH      = .4 CH3NCH3 + .5 DMAO2 + .1 CH2NCH3 + .1 HO2        : {%TrGAmi} (1-ya_soan1-ya_soan2-ya_soan5)*k_DMA_OH*xgcamin; {&26220}
<G4220bMK> DMA    + OH      = BSOV + 2 LCARBON + LNITROGEN                       : {%TrGAmi} ya_soan1*k_DMA_OH; {&26220}
<G4220cMK> DMA    + OH      = BLOV + 2 LCARBON + LNITROGEN                       : {%TrGAmi} ya_soan2*k_DMA_OH; {&26220}
<G4220dMK> DMA    + OH      = BELV + 2 LCARBON + LNITROGEN                       : {%TrGAmi} ya_soan5*k_DMA_OH; {&26220}
<G4221MK>  CH3NCH3 +  NO    = NDMA                                               : {%TrGAmi} 2.39E-13{§} *xgcnno;       {&2596}  
<G4222MK>  CH3NCH3 +  O2    = CH2NCH3  + HO2                                     : {%TrGAmi} 9.54E-20{§};       {&2596}
<G4223MK>  CH3NCH3 +  NO2   = .6 CH2NCH3 + .7 HONO + .3 DMNNO2 + .1 CH3NO + .1 CH3O2 : {%TrGAmi} 3.18E-13{§} *xgcnno;   {&2595}
<G4224MK>  DMNNO2 + OH      = CH2NCH3  + NO2 + H2O                               : {%TrGAmi} 4.5E-12{§};        {&26220}
<G4225MK>  DMAO2  + NO      = CH3NHCHO + 0.5 NO2 + 0.5 HO2 + 0.5 HONO            : {%TrGAmi} 8.5E-12{§};        {&26220}
<G4226MK>  NDMA   + OH      = CH2NCH3  + NO  + H2O                               : {%TrGAmi} 3.0E-12{§};        {&26220}
#ENDREPLACE

#REPLACE <>
<G4230aMK> CH2NCH3 + OH     = CH3NHCHO + HO2                                     : {%TrGAmi} (1-ya_soan1-ya_soan2-ya_soan5)*k_MMI_OH; {&26220}
<G4230bMK> CH2NCH3 + OH     = BSOV + 2 LCARBON + LNITROGEN                       : {%TrGAmi} ya_soan1*k_MMI_OH; {&26220}
<G4230cMK> CH2NCH3 + OH     = BLOV + 2 LCARBON + LNITROGEN                       : {%TrGAmi} ya_soan2*k_MMI_OH; {&26220}
<G4230dMK> CH2NCH3 + OH     = BELV + 2 LCARBON + LNITROGEN                       : {%TrGAmi} ya_soan5*k_MMI_OH; {&26220}
#ENDREPLACE


// 3C (#G43nn)
#REPLACE <>
<G4300aMK> TMA     + OH     = TMAO2 + H2O             : {%TrGAmi} (1-ya_soan1-ya_soan2-ya_soan5)*k_TMA_OH*xgcamin; {&26220}
<G4300bMK> TMA     + OH     = BSOV + 3 LCARBON + LNITROGEN                       : {%TrGAmi} ya_soan1*k_TMA_OH; {&26220}
<G4300cMK> TMA     + OH     = BLOV + 3 LCARBON + LNITROGEN                       : {%TrGAmi} ya_soan2*k_TMA_OH; {&26220}
<G4300dMK> TMA     + OH     = BELV + 3 LCARBON + LNITROGEN                       : {%TrGAmi} ya_soan5*k_TMA_OH; {&26220}
<G4301MK>  TMAO2   + NO     = .3 DMNCHO + .3 HONO + .7 TMAO + .7 NO2 + .7 HO2    : {%TrGAmi} 8.5E-12;           {&26220}
<G4302MK>  TMAO    + O2     = DMNCHO    + HO2                                    : {%TrGAmi} 2.4E-15{§}*5;      {&26220}
<G4303MK>  TMAO             = CH3NCH3   + HCHO                                   : {%TrGAmi} 4.0E+05{§};        {&26220}
<G4304MK>  DMNCHO  + OH     = DMNCHOO2                                           : {%TrGAmi} 1.4E-11{§}*0.25;   {&26220}
<G4305MK>  DMNCHOO2 + NO    = TMADF     + NO2 + HO2                              : {%TrGAmi} 8.5E-12{§};        {&26220}
<G4306MK>  HOETNHCHO + OH   = HOCH2CONHCHO + HO2                                 : {%TrGAmi} k_HAC_OH;          {&26220}
<G4310MK>  DMCNH2    + O2   = 0.8 DMCOONH2 + 0.1 CH2CNH2CH3 + 0.1 DMCNH + HO2    : {%TrGAmi}  k_RO_O2*0.25;     {&2624}
<G4311MK>  DMCOONH2  + NO   = H2NCOCH3 + HCHO + NO2                              : {%TrGAmi}  KAPNO;            {&2624}
<G4312MK>  CH3CNH2MOH + NO  = 0.5 H2NCOCH3 + 0.5 H2NCOCH2OH  + CO2 + NO2         : {%TrGAmi}  KAPNO;            {&2624}
<G4313MK>  CH3CNH2MOH + O2  = 0.8 HNCCH3MOH + 0.1 H2NCCHOHCH3 + 0.1 H2NCCH2MOH + HO2 : {%TrGAmi}  k_RO_O2*0.25; {&2624}
#ENDREPLACE

#REPLACE <>
<G4320MK>  CH3CHOCH3 + O2   = CH3COCH3 + HO2                                     : {%TrG} k_RO_O2;            {&2419}
#ENDREPLACE


// 4C (#G44nn)
#REPLACE <>
<G4400MK>  DEA    + OH      = .4 HOETNETOH + .5 DEAO2 + .1 HOETNHCH2CHO + .1 HO2 : {%TrGAmi} k_DEA_OH*xgcamin;      {&26220}
<G4401MK>  HOETNETOH + NO   = NDELA                                              : {%TrGAmi} 2.39E-13{§} *xgcnno;   {&2596}  
<G4402MK>  HOETNETOH + O2   = HOCH2CHNETOH  + HO2                                : {%TrGAmi} 9.54E-20{§};           {&2596}
<G4403MK>  HOETNETOH + NO2  = .6 HOCH2CHNETOH + .7 HONO + .3 DEANNO2 + .1 HOCH2CH2NO + .1 HOCH2CH2O2 : {%TrGAmi} 3.18E-13{§} *xgcnno;   {&2595}
<G4404MK>  DEANNO2 + OH     = HOCH2CHNETOH  + NO2 + H2O                          : {%TrGAmi} 1.74E-11{§};       {&26220}
<G4405MK>  DEAO2  + NO      = HOETNHCHO + HCHO + 0.5 NO2 + 0.5 HO2 + 0.5 HONO    : {%TrGAmi} 8.5E-12{§};        {&26220}
<G4406MK>  NDELA  + OH      = HOCH2CHNETOH  + NO  + H2O                          : {%TrGAmi} 1.61E-11{§};       {&26220}
<G4407MK>  HOCH2CHNETOH + OH = HOCH2CONETOH + HO2                                : {%TrGAmi} k_MMI_OH;          {&26220}
#ENDREPLACE

#REPLACE <>
<G4410aMK> AMP   + OH       = 0.30 AMPN + 0.65 DMCNH2CHO + 0.5 HO2 + 0.05 NH3 + 0.05 MACR  : {%TrGAmi} (1-ya_soan1-ya_soan2-ya_soan5)*k_AMP_OH*fkoh_mea; {&2623}
<G4410bMK> AMP   + OH       = BSOV + 4 LCARBON + LNITROGEN                       : {%TrGAmi} ya_soan1*k_AMP_OH*fkoh_mea; {&2623}
<G4410cMK> AMP   + OH       = BLOV + 4 LCARBON + LNITROGEN                       : {%TrGAmi} ya_soan2*k_AMP_OH*fkoh_mea; {&2623}
<G4410dMK> AMP   + OH       = BELV + 4 LCARBON + LNITROGEN                       : {%TrGAmi} ya_soan5*k_AMP_OH*fkoh_mea; {&2623}
<G4411MK>  AMPN   + NO      = NAMP                                               : {%TrGAmi} 0.26*3.18E-13{§};  {&2624} 
<G4412MK>  AMPN   + NO2     = AMPNNO2                                            : {%TrGAmi} 3.18E-13{§};       {&2595}
<G4413MK>  AMPN   + O3      = AMPOX                                              : {%TrGAmi} 1.7E-13{§};        {&2624}  
<G4414MK>  DMCNH2CHO + OH   = DMCNH2CO3 + H2O                                    : {%TrGAmi} k_AAC_OH;          {&2624}
<G4415MK>  DMCNH2CO3 + NO   = DMCNH2 + CO2 + NO2                                 : {%TrGAmi} KAPNO;             {&2624}
<G4416MK>  DMCNH2CO3 + NO2  = AMPAN                                              : {%TrGAmi} k_CH3CO3_NO2;      {&1945}
<G4417MK>  AMPAN     {+M}   = DMCNH2CO3 + NO2                                    : {%TrGAmi} k_PAN_M;           {&1945}
<G4418MK>  AMPO    + O2     = DMOCNH2MOH + HO2                                   : {%TrGAmi} k_RO_O2*0.25;      {&2624}
<G4419MK>  AMPO             = CH3CNH2MOH + HCHO                                  : {%TrGAmi} k_DEC*0.2;         {&2624}
<G4420MK>  AMPNNO2 + OH     = AMPNA + HO2                                        : {%TrGAmi} k_NITRAMA_OH;      {&2624}
<G4421MK>  AMP   + NO3      = 0.5 AMPN + 0.4 DMCNH2CHO + 0.1 AMPO + 0.5 HO2 + HNO3   : {%TrGAmi}  k_AMP_NO3;    {&2625}
#ENDREPLACE


// 5C (#G56nn)
#REPLACE <>
<G4500MK>  DEANCH2O2 + NO   = DEANCHO + NO2 + HO2                                : {%TrGAmi} 8.5E-12{§};        {&26220}
#ENDREPLACE


// 6C (#G46nn)
#REPLACE <>
<G4600MK>  TEA     + OH     = TEAO2 + H2O                                        : {%TrGAmi} k_TEA_OH*xgcamin;  {&26220}
<G4601MK>  TEAO2   + NO     = .3 DEANCH2CHO + .3 HONO + .7 TEAO + .7 NO2 + .7 HO2  : {%TrGAmi} 8.5E-12;         {&26220}
<G4602MK>  TEAO    + O2     = DEANCOCH2OH    + HO2                               : {%TrGAmi} 2.4E-15{§}*5;      {&26220}
<G4603MK>  TEAO             = HOETNETOH  + CH3CHO                                : {%TrGAmi} 4.0E+05{§};        {&26220}
<G4604MK>  TEAO    + O2     = DEANCH2O2  + HCHO                                  : {%TrGAmi} 2.4E-15{§};        {&26220}
<G4605MK>  DEANCH2CHO  + OH = DEANCH2COO2                                        : {%TrGAmi} 1.4E-11{§};        {&26220}
<G4606MK>  DEANCH2COO2 + NO = DEANCHO + HCHO     + NO2 + HO2                     : {%TrGAmi} 8.5E-12{§};        {&26220}
#ENDREPLACE

#REPLACE <>
<G4610MK>  TME    + O3      = CH3COCH3 + CH3COCH2O2 + OH                         : {%TrG} 3.03E-15{§}*EXP(-294./temp);   {&2419}
<G4611MK>  TME    + OH      = TMEO2                                              : {%TrG} 1.1E-10{§};         {&2419}
<G4612MK>  TMEO2  + NO      = 2.0 CH3COCH3 + NO2 + HO2                           : {%TrG} KRO2NO;             {&2419}
#ENDREPLACE

#REPLACE <>
<G4620MK>  CHEX   + OH      = CHEXO2                                             : {%TrG} 2.88E-17{§}*temp*temp*exp(309./temp); {&2419} 
<G4621MK>  CHEXO2           = 0.6 CHEXO + 0.2 CHEXOL + 0.2 CHEXONE               : {%TrG} 9.20E-14{§}*RO2;    {&2419} 
<G4622MK>  CHEXO2 + HO2     = CHEXOOH                                            : {%TrG} 0.770*KRO2HO2(6);   {&2419}
<G4623MK>  CHEXO2 + NO      = CHEXO + NO2                                        : {%TrG} KRO2NO;             {&2419}
<G4624MK>  CHEXO  + O2      = CHEXONE + HO2                                      : {%TrG} k_RO_O2;            {&2419}
#ENDREPLACE



// ------------------------------------------------------------------------------

//   SOA formation reactions

// ------------------------------------------------------------------------------


// isoprene oxidation:
// 5C (#G45nnn)
#REPLACE <G45012>
<>  LISOPACO2  + HO2 = LISOPACOOH + 0.024 BLOV + 0.119 BSOV                      : {%TrG} KRO2HO2(5); {&2419}
#ENDREPLACE
#REPLACE <G45013a>
<> LISOPACO2  + NO  = LISOPACO + NO2 + 0.003 BLOV  + 0.101 BSOV                    : {%TrG} KRO2NO*(1.-alpha_AN(6,1,0,0,0,temp,cair)); {&3158, 3159, 3264}
#ENDREPLACE
#REPLACE <G45016>
<>  LDISOPACO2 + HO2 = LISOPACOOH + 0.024 BLOV + 0.119 BSOV                      : {%TrG} KRO2HO2(5); {&2419}
#ENDREPLACE
#REPLACE <G45017a>
<> LDISOPACO2 + NO  = LDISOPACO + NO2 + 0.003 BLOV  + 0.101 BSOV                   : {%TrG} KRO2NO*(1.-alpha_AN(6,1,0,0,0,temp,cair)); {&3158, 3159, 3264}
#ENDREPLACE
#REPLACE <G45023b>
<> ISOPBO2    + HO2 = MVK + HCHO + HO2 + OH + 0.024 BLOV + 0.119 BSOV            : {%TrG} KRO2HO2(5)*rchohch2o2_oh; {&3264}
#ENDREPLACE
#REPLACE <G45024a>
<> ISOPBO2    + NO  = MVK + HCHO + HO2 + NO2 + 0.003 BLOV  + 0.101 BSOV            : {%TrG} KRO2NO*(1.-alpha_AN(6,3,0,0,0,temp,cair)); {&3158, 3264}
#ENDREPLACE
#REPLACE <G45031b>
<> ISOPDO2    + HO2 = MACR + HCHO + HO2 + OH + 0.024 BLOV + 0.119 BLOV           : {%TrG} KRO2HO2(5)*rchohch2o2_oh; {&3264}
#ENDREPLACE
#REPLACE <G45032a>
<> ISOPDO2    + NO  = MACR + HCHO + HO2 + NO2 + 0.003 BLOV + 0.101 BSOV            : {%TrG} KRO2NO*(1.-alpha_AN(6,2,0,0,0,temp,cair)); {&3158, 3264}
#ENDREPLACE


//*----- aromatic SOA ----------------------------------- ;
//* Yields from Couvidat et al., JGR (2012): AnCIP and AnBIP ;
//KRO2NO            [OXYO2] + [NO]        =  0.063 ALOC ;
//KHO2RO2           [OXYO2] + [HO2]       =  0.710 ALOC ;
//* end MAFOR reactions

// toluene oxidation
// 7C (#G47nnn)
// aromatics-related (G47400...G47599):
#REPLACE <G47400>
<> TOLUENE    + OH     = .07 C6H5CH2O2 + .18 CRESOL + .18 HO2 + .65 TLBIPERO2 + .10 TLEPOXMUC + .10 HO2 + .04 AELV : {%TrGAro} 1.8E-12*EXP(340/TEMP); {&2419} // Merged.
#ENDREPLACE

#REPLACE <G47407b>
<> TLBIPERO2  + HO2    = OH + .6 GLYOX + .4 MGLYOX + HO2 + .2 C4MDIAL + .2 C5DICARB + .2 TLFUONE + .2 BZFUONE + .2 MALDIAL + 0.780 ALOV  : {%TrGAro} KRO2HO2(7)*rbipero2_oh; {&2419, 3188} // According to \citet{3188}, the branching ratio rbipero2\_oh is set to 0.4 in order to take into account the OH-recycling and summed yield of butendial and methylbutendial.
#ENDREPLACE

#REPLACE <G47408a>
<> TLBIPERO2  + NO     = NO2 + .6 GLYOX + .4 MGLYOX + HO2 + .2 C4MDIAL + .2 C5DICARB + .2 TLFUONE + .2 BZFUONE + .2 MALDIAL + 0.097 ALOV + 0.748 ASOV : {%TrGAro} KRO2NO*(1.-alpha_AN(11,2,0,0,1,temp,cair)); {&2419} // KDEC TLBIPERO $\rightarrow$ 0.6 GLYOX + 0.4 MGLYOX + HO2 + 0.2 C4MDIAL + 0.2 C5DICARB + 0.2 TLFUONE + 0.2 BZFUONE + 0.2 MALDIAL
#ENDREPLACE


// xylene oxidation
// 8C (#G48nnn)
// aromatics-related (G48400...G48599):
#REPLACE <G48400a>
<> LXYL        + OH    = TLEPOXMUC + HO2  + LCARBON + 0.063 ALOV + 0.424 ASOV    : {%TrGAro} 0.401E-11; {&2419} // Same products as for toluene. Assuming a 1:1:1 proportion in xylenes emissions the analogous toluene product is produced with a rate constant equal to (1.36E-11*0.24 + 2.31E-11*0.29 + 1.43E-11*0.155)/3, where k and coefficients are for the single isomers ortho, meta and para from MCM.
#ENDREPLACE


// TMB oxidation
// 9C (#G49nnn)
#REPLACE <G49400a>
<> LTMB       + OH     = TLBIPERO2 +  HO2 + 2 LCARBON + .04 ALOV                 : {%TrGAro} 2.917E-11; {&2419} // Same products as for toluene. Assuming a 1:1:1 proportion in xylenes emissions the analogous toluene product is produced with a rate constant equal to (3.27E-11*0.70 + 3.25E-11*0.61 + 5.67E-11*0.79)/3, where k and coefficients are for the single isomers 1,2,3-, 1,3,4- and 1,3,5- from MCM.
#ENDREPLACE


// monoterpene oxidation
// >=10C (#G40nnn)
// terpene-related (G40200...G40399)
// APINENE + OH
#REPLACE <G40201a>
<> LAPINABO2 + NO         = PINAL + HO2 + NO2 + 0.052 BLOV + 0.184 BSOV          : {%TrGTer} KRO2NO*(1.-(.65*alpha_AN(11,3,0,0,0,temp,cair)+.35*alpha_AN(11,2,0,0,0,temp,cair)));     {&2419, 3264}
#ENDREPLACE
#REPLACE <G40202b>
<> LAPINABO2 + HO2       = PINAL + HO2 + OH                                      : {%TrGTer} (1-ya_soan1-ya_soan2)*KRO2HO2(10)*rchohch2o2_oh; {&2419, 3264}
#ENDREPLACE
#REPLACE <>
<G40202c>  LAPINABO2 + HO2   = BSOV + 10 LCARBON                                 : {%TrGTer} ya_soan1*KRO2HO2(10)*rchohch2o2_oh; {&2419, 3264}
#ENDREPLACE
#REPLACE <>
<G40202d>  LAPINABO2 + HO2   = BLOV + 10 LCARBON                                 : {%TrGTer} ya_soan2*KRO2HO2(10)*rchohch2o2_oh; {&2419, 3264}
#ENDREPLACE


// APINENE + O3
#REPLACE <G40232>
<>  APINENE + O3          = .09 APINBOO + .08 PINONIC + .77 OH + .33 NORPINAL + .33 CO + .33 HO2 + .06 APINAOO + .44 C109O2 + 0.07 BELV + 0.23 BLOV : {%TrGTer} 8.05E-16{§§0.15}*EXP(-640./TEMP); {&1745, 2638} // Products from \citet{3028}. BELV from \citet{2638}
#ENDREPLACE

#REPLACE <G40235>
<>  APINAOO + NO          = PINAL + NO2 + 0.052 BLOV + 0.184 BSOV                : {%TrGTer} 1.00E-14{§}; {&2419}
#ENDREPLACE

#REPLACE <G40239>
<>  APINBOO + NO          = PINAL + NO2 + 0.052 BLOV + 0.184 BSOV                : {%TrGTer} 1.00E-14{§}; {&2419}
#ENDREPLACE

#REPLACE <G40242>
<>  C109O2 + NO           = C89CO3 + HCHO + NO2 + 0.052 BLOV + 0.184 BSOV        : {%TrGTer} KRO2NO; {&2419} // \alkylnitrateneglected
#ENDREPLACE
#REPLACE <G40243b>
<>  C109O2 + HO2          = C89CO3 + HCHO + OH                                   : {%TrGTer} (1-ya_soan1-ya_soan2)*KRO2HO2(10)*rcoch2o2_oh; {&2419, 3264}
#ENDREPLACE
#REPLACE <>
<G40243c>  C109O2 + HO2      = BSOV + 10 LCARBON                                 : {%TrGTer} ya_soan1*KRO2HO2(10)*rcoch2o2_oh; {&2419, 3264}
#ENDREPLACE
#REPLACE <>
<G40243d>  C109O2 + HO2      = BLOV + 10 LCARBON                                 : {%TrGTer} ya_soan2*KRO2HO2(10)*rcoch2o2_oh; {&2419, 3264}
#ENDREPLACE



// BPINENE + OH
#REPLACE <G40254a>
<> BPINAO2 + NO          = NOPINONE + HCHO + HO2 + NO2 + 0.052 BLOV + 0.184 BSOV  : {%TrGTer} KRO2NO*(1.-alpha_AN(11,3,0,0,0,temp,cair)); {&2419, 3264}
#ENDREPLACE
#REPLACE <G40253b>
<> BPINAO2 + HO2         = NOPINONE + HCHO + HO2 + OH                            : {%TrGTer} (1-ya_soan1-ya_soan2)*KRO2HO2(10)*rcoch2o2_oh; {&2419, 3264}
#ENDREPLACE
#REPLACE <>
<G40253c>  BPINAO2 + HO2         = BSOV + 10 LCARBON                             : {%TrGTer} ya_soan1*KRO2HO2(10)*rcoch2o2_oh; {&2419, 3264}
#ENDREPLACE
#REPLACE <>
<G40253d>  BPINAO2 + HO2         = BLOV + 10 LCARBON                             : {%TrGTer} ya_soan2*KRO2HO2(10)*rcoch2o2_oh; {&2419, 3264}
#ENDREPLACE

#REPLACE <G40258a>
<> ROO6R1O2 + NO         = ROO6R3O2 + CH3COCH3 + NO2 + 0.052 BLOV + 0.184 BSOV   : {%TrGTer} KRO2NO*(1.-alpha_AN(13,3,0,0,0,temp,cair)); {&3019}
#ENDREPLACE
#REPLACE <G40259>
#ENDREPLACE
#REPLACE <>
<G40259a>  ROO6R1O2 + HO2        = 10 LCARBON                                    : {%TrGTer} (1-ya_soan1-ya_soan2)*KRO2HO2(10); {&3019} // ROO6R1OOH is produced but no sink for it.
#ENDREPLACE
#REPLACE <>
<G40259b>  ROO6R1O2 + HO2        = BSOV + 10 LCARBON                             : {%TrGTer} ya_soan1*KRO2HO2(10); {&2419, 3264}
#ENDREPLACE
#REPLACE <>
<G40259ac>  ROO6R1O2 + HO2       = BLOV + 10 LCARBON                             : {%TrGTer} ya_soan2*KRO2HO2(10); {&2419, 3264}
#ENDREPLACE


// BPINENE + O3
#REPLACE <G40267a>
<> BPINENE + O3          = NOPINONE + .63 CO + .37 CH2OO + .16 OH + .16 HO2  + 0.07 BELV + 0.23 BLOV  : {%TrGTer} 1.35E-15{§§0.25}*EXP(-1270./TEMP)*.051/(1-.027); {&1745} // Products from \citet{3018}.
#ENDREPLACE


// ------------------------------------------------------------------------------

//   SOA ageing reactions

// ------------------------------------------------------------------------------

// biogenics-related (G40600...G40699):
#REPLACE <>
<G40600>  BSOV + OH      = BLOV         : {%TrGTer} 4.0E-11; {&2637}
#ENDREPLACE
#REPLACE <>
<G40601>  BLOV + OH      = BELV         : {%TrGTer} 4.0E-11; {&2637}
#ENDREPLACE
#REPLACE <>
<G40602>  BSOV           = BELV         : {%TrGTer} 9.6E-06; {&2639}
#ENDREPLACE
#REPLACE <>
<G40603>  BLOV           = BELV         : {%TrGTer} 9.6E-06; {&2639}
#ENDREPLACE

// aromatics-related (G40700...G40799):
#REPLACE <>
<G40700>  ASOV + OH      = ALOV         : {%TrGTer} 4.0E-11; {&2637}
#ENDREPLACE
#REPLACE <>
<G40701>  ALOV + OH      = AELV         : {%TrGTer} 4.0E-11; {&2637}
#ENDREPLACE
#REPLACE <>
<G40702>  ASOV           = AELV         : {%TrGTer} 9.6E-06; {&2639}
#ENDREPLACE
#REPLACE <>
<G40703>  ALOV           = AELV         : {%TrGTer} 9.6E-06; {&2639}
#ENDREPLACE
#REPLACE <>
<G40704>  PIOV + OH      = PSOV         : {%TrGTer} 2.0E-11; {&2636}
#ENDREPLACE
#REPLACE <>
<G40705>  PSOV + OH      = PELV         : {%TrGTer} 2.0E-11; {&2636}
#ENDREPLACE
#REPLACE <>
<G40706>  PELV           = PSOV         : {%TrGTer} 5.0E-04; {&2640}
#ENDREPLACE


// ------------------------------- S -------------------------

// MAFOR DMS CHEMISTRY
// S + C (#G94nn)

#REPLACE <G9200>
<> SO2 + OH     = HSO3                  : {%StTrG} k_3rd(temp,cair,3.E-31,-3.3,1.5E-12,0.,0.6); {&3245}
#ENDREPLACE

// To add a new reaction, specify an empty equation tag
// in the "#REPLACE" command ("<>"), and give a complete
// equation tag in the replacement ("<G9876>"):
//#REPLACE <>
//<G9876JD> Dummy + OH = CH3O2 + H2O : {%StG} 1.57E-13; {&&}
//#ENDREPLACE

#REPLACE <>
<G9201MK>  HSO3 + O2    = HO2 + SO3     : {%StTrG} 1.3E-12*exp(-330/temp); {&3245}
#ENDREPLACE
#REPLACE <>
<G9202MK>  SO3 + H2O    = H2SO4         : {%StTrG} 2.4E-15;  {&2634}
#ENDREPLACE

#REPLACE <G9400a>
<> DMS     + OH     = CH3SCH2 + H2O     : {%TrG} 1.13E-11{§§0.10}*EXP(-253./temp); {&746}
#ENDREPLACE
#REPLACE <G9400b>
<> DMS     + OH     = DMSOH + HO2       : {%TrG} k_DMS_OH; {&746}
#ENDREPLACE
#REPLACE <G9401>
<> DMS     + NO3    = CH3SCH2 + HNO3    : {%TrG} 1.9E-13{§§0.15}*EXP(520./temp); {&2591}
#ENDREPLACE
// It is possible to create several branches of the reaction:
// Equation tags in the replacement (<a>, <b>, ...) will be added as a suffix
// to the original equation tag, i.e.:
// <a> --> <G4110a>
#REPLACE <G9402>
<a> DMSO    + OH     = DMSOHO           : {%TrG} k_DMSO_OH_B1; {&2591}
<b> DMSO    + OH     = CH3SOCH2 + H2O   : {%TrG} k_DMSO_OH_B2; {&2591}
#ENDREPLACE
#REPLACE <G9403>
<>  CH3SO2           = SO2 + CH3O2      : {%TrG} 1.0E13*EXP((-16500.*fch3so2/1.98635)/temp); {&2591}
#ENDREPLACE
#REPLACE <G9404>
<>  CH3SO2  + O3     = CH3SO3 + O2      : {%TrG} k_CH3SO2_O3; {&2591}
#ENDREPLACE
#REPLACE <G9405>
<>  CH3SO3  + HO2    = CH3SO3H + O2     : {%TrG} k_CH3SO3_HO2; {&2591}
#ENDREPLACE

#REPLACE <>
<G9420aMK> DMSOH   + O2     = DMSO + HO2           : {%TrG} 5.0E-13; {&2591}
<G9420bMK> DMSOH   + O2     = DMSOHOO              : {%TrG} 3.0E-14; {&2591}
<G9421MK>  DMSOH            = CH3SOH + CH3O2       : {%TrG} 5.0E+5;  {&2591}
<G9422MK>  DMSOHOO + NO     = DMSOHO + NO2         : {%TrG} 5.0E-12; {&2591}
<G9423MK>  DMSOHO  + O2     = DMSO2 + HO2          : {%TrG} 1.0E-13; {&2591}
<G9424MK>  DMSOHO           = CH3SOOH + CH3O2      : {%TrG} 6.0E+5;  {&2591}
<G9425MK>  CH3SOCH2 + O2    = DMSOOO               : {%TrG} 0.5E-13; {&2591}
<G9426MK>  DMSOOO  + NO     = CH3SO + HCHO + NO2   : {%TrG} 1.0E-11; {&2591}
<G9427MK>  CH3SCH2 + O2     = DMSOO                : {%TrG} 5.7E-12; {&2591}
<G9428MK>  DMSOO   + NO     = HCHO + CH3S + NO2    : {%TrG} 1.9E-11; {&2591}
<G9429MK>  DMSOO   + HO2    = DMSOOH + O2          : {%TrG} 1.5E-12; {&2591}
<G9430MK>  DMSOO   + CH3O2  = CH3S + 2 HCHO + HO2  : {%TrG} 1.8E-13; {&2591}
<G9431MK>  CH3S    + O3     = CH3SO + O2           : {%TrG} 1.36E-12*EXP(374./temp); {&2591}
<G9432MK>  CH3S    + NO2    = CH3SO + NO           : {%TrG} 2.8E-11*EXP(240./temp); {&2591}
<G9433aMK> CH3S    + O2     = CH3SOO               : {%TrG} 1.5E-14; {&2591}
<G9433bMK> CH3S    + O2     = CH3O2 + SO2          : {%TrG} 3.0E-18; {&2591}
<G9434MK>  CH3S    + CH3O2  = CH3SO + HCHO + HO2   : {%TrG} 6.1E-11; {&2591}
<G9435MK>  CH3SO   + O3     = 0.5 CH3SO2 + 0.5 O2 + 0.5 CH3O2 + 0.5 SO2  : {%TrG} 3.2E-13; {&2591}
<G9436aMK> CH3SO   + NO2    = CH3SO2 + NO          : {%TrG} 1.2E-11; {&2591}
<G9436bMK> CH3SO   + NO2    = CH3O2 + SO2 + NO     : {%TrG} 3.5E-12; {&2591}
<G9437MK>  CH3SO   + O2     = CH3SOO2              : {%TrG} 7.7E-18; {&2591}
<G9438MK>  CH3SO   + CH3O2  = CH3SO2 + HCHO + HO2  : {%TrG} 3.0E-12; {&2591}
<G9439MK>  CH3SOH  + OH     = CH3SO + H2O          : {%TrG} 1.10E-10;{&2591}
<G9440MK>  CH3SOH  + NO3    = CH3SO + HNO3         : {%TrG} 3.4E-12; {&2591}
<G9441MK>  CH3SOH  + HO2    = CH3SO + H2O2         : {%TrG} 8.5E-13; {&2591}
<G9442MK>  CH3SOH  + CH3O2  = CH3SO + CH3OOH       : {%TrG} 8.5E-13; {&2591}
<G9443MK>  CH3SOO  + O3     = CH3SO + 2O2          : {%TrG} 8.0E-13; {&2591}
<G9444MK>  CH3SOO  + NO     = CH3SO + NO2          : {%TrG} 1.1E-11; {&2591}
<G9445MK>  CH3SOO  + O2     = CH3O2 + SO2          : {%TrG} 2.0E-17; {&2591}
<G9446aMK> CH3SOO           = CH3S + O2            : {%TrG} 1.0E+5;  {&2591}
<G9446bMK> CH3SOO           = CH3SO2               : {%TrG} 8.0;     {&2591}
<G9447MK>  CH3SOO + CH3O2   = CH3SO + HCHO + HO2   : {%TrG} 5.5E-12; {&2591}
<G9448MK>  CH3SOOH + OH     = CH3SO2 + H2O         : {%TrG} k_MSIA_OH; {&2591}
<G9449MK>  CH3SOOH + NO3    = CH3SO2 + HNO3        : {%TrG} 1.0E-13; {&2591}
<G9450MK>  CH3SOOH + HO2    = CH3SOO + H2O2        : {%TrG} 1.0E-15; {&2591}
<G9451MK>  CH3SOOH + CH3O2  = CH3SO2 + CH3OOH      : {%TrG} 1.0E-15; {&2591}
<G9452MK>  CH3SO2  + HO2    = CH3SO3 + OH          : {%TrG} 2.5E-13; {&2591}
<G9453MK>  CH3SO2  + NO2    = CH3SO3 + NO          : {%TrG} 2.2E-11; {&2591}
<G9454MK>  CH3SO2 + CH3O2   = CH3SO3 + HCHO + HO2  : {%TrG} 2.5E-13; {&2591}
<G9455MK>  CH3SO3  + NO2    = MSOON                : {%TrG} 3.0E-15; {&2591}
<G9456MK>  CH3SO3           = SO3   + CH3O2        : {%TrG} k_CH3SO3_dec; {&2591}
<G9457MK>  CH3SOO2 + NO     = CH3SO2 + NO2         : {%TrG} 1.0E-11; {&2591}
<G9458MK>  CH3SOO2 + HO2    = CH3SOO2H + O2        : {%TrG} 3.0E-12; {&2591}
<G9459MK>  CH3SOO2          = CH3SO + O2           : {%TrG} 170.;    {&2591}
<G9460MK>  CH3SOO2 + CH3O2  = CH3SO2 + HCHO + HO2  : {%TrG} 5.5E-12; {&2591}
<G9461MK>  CH3SO4  + NO     = CH3SO3 + NO2         : {%TrG} 1.0E-11; {&2591}
<G9462MK>  CH3SO4  + HO2    = CH3SO4H              : {%TrG} 2.0E-12; {&2591}
<G9463MK>  CH3SO4           = CH3SO2 + O2          : {%TrG} 170.;    {&2591}
<G9464MK>  CH3SO4 + CH3O2   = CH3SO3 + HCHO + HO2  : {%TrG} 5.5E-12; {&2591}
<G9465MK>  CH3SO4  + NO2    = MSPN                 : {%TrG} 5.89E-12;{&2591}
<G9466MK>  MSOON            = CH3SO3 + NO2         : {%TrG} 1.15E-2; {&2591}
<G9467MK>  MSPN             = CH3SO4 + NO2         : {%TrG} 1.15E-2; {&2591}
<G9468MK>  DMSO2   + OH     = DMSO2OO              : {%TrG} k_DMSO2_OH; {&2591}
<G9469MK>  DMSO2OO + NO     = DMSO2O + NO2         : {%TrG} 5.0E-12;    {&2591}
<G9470MK>  DMSO2OO + HO2    = DMSO2OOH + O2        : {%TrG} 1.5E-12;    {&2591}
<G9471MK>  DMSO2O           = CH3SO2 + HCHO        : {%TrG} 1.0E+1;     {&2591}
<G9472MK>  CH3SO3H + H2O    = MSAH2O               : {%TrG} 6.58E-10;   {&2633}
<G9473MK>  MSAH2O           = CH3SO3H + H2O        : {%TrG} 5.53E08*EXP(-669.71/temp); {&2633}
#ENDREPLACE


// MAFOR DMS + AMINES CHEMISTRY
// MSADMA, MSADMAH2O, MSATMA, MSATMAH2O are nucleation precursors
// of MSA-DMA-(H2O) or of MSA-TMA-(H2O) ternary nucleation 
// S + N (#G99nn)

#REPLACE <>
<G9900MK>  DMA + MSAH2O     = MSADMAH2O            : {%TrGAmi} 1.32E-09;   {&2633}
<G9901MK>  MSADMAH2O        = DMA + MSAH2O         : {%TrGAmi} 30.9   *EXP(-4118.74/temp);   {&2633}
<G9902MK>  MSADMAH2O        = MSADMA + H2O         : {%TrGAmi} 9.96E08*EXP(-736.69/temp);    {&2633}
<G9903MK>  MSADMA + H2O     = MSADMAH2O            : {%TrGAmi} 1.66E-09;   {&2633}
#ENDREPLACE

#REPLACE <>
<G9910MK>  TMA + MSAH2O     = MSATMAH2O            : {%TrGAmi} 1.44E-09;   {&2633}
<G9911MK>  MSATMAH2O        = TMA + MSAH2O         : {%TrGAmi} 5.29   *EXP(-4487.08/temp);    {&2633}
<G9912MK>  MSATMAH2O        = MSATMA + H2O         : {%TrGAmi} 7.91E06*EXP(-1674.28/temp);    {&2633}
<G9913MK>  MSATMA + H2O     = MSATMAH2O            : {%TrGAmi} 1.49E-09;   {&2633}
#ENDREPLACE




// ------------------------------- hv ------------------------

// MAFOR PHOTOLYSIS REACTIONS

// 2C (#J42nn)
#REPLACE <>
<J4200MK>  H2NCH2CHO + hv = MMAO2 + HCHO           : {%TrGAJ} jx(ip_CH3CHO){§};   {&2594}
<J4201MK>  H2NCOCHO  + hv = H2NCHO + HCHO          : {%TrGAJ} jx(ip_CH3CHO){§};   {&2594}
<J4202MK>  MEANNO    + hv = MEAN + NO              : {%TrGAJ} 0.33*jx(ip_NO2){§}; {&2594}
<J4203MK>  NDMA      + hv = CH3NCH3 + NO           : {%TrGAJ} 0.25*jx(ip_NO2){§}; {&2594}
#ENDREPLACE

// 3C (#J43nn)
#REPLACE <>
<J4300MK>  IPN       + hv = CH3CHOCH3 + NO         : {%TrGJ} 0.08*jx(ip_NO2);     {&26220}
#ENDREPLACE

// 4C (#J44nn)
#REPLACE <>
<J4400MK>  NDELA      + hv = HOETNETOH + NO        : {%TrGAJ} 0.33*jx(ip_NO2){§}; {&2594}
<J4401MK>  NAMP       + hv = AMPN + NO             : {%TrGAJ} 0.34*jx(ip_NO2){§}; {&2624}
<J4402MK>  DMOCNH2MOH + hv = CH3CNH2MOH + HCHO     : {%TrGAJ} jx(ip_CH3CHO){§};   {&26220}
<J4403MK>  DMCNH2CHO  + hv = DMCNH2 + HCHO         : {%TrGAJ} jx(ip_CH3CHO){§};   {&26220}
#ENDREPLACE

// 6C (#J46nn)
#REPLACE <>
<J4600MK>  CHEXONE   + hv = C2H4 + C3H6 + CO       : {%TrGJ} 0.42*jx(ip_CHOH){§}; {&1584}
#ENDREPLACE


// ------------------------------------------------------------------------------

